#!rsc
# RouterOS script: unattended-lte-firmware-upgrade
# Copyright (c) 2018-2020 Christian Hesse <mail@eworm.de>
#
# schedule unattended lte firmware upgrade

:foreach Interface in=[ / interface lte find ] do={
  :local Firmware;
  :local IntName [ / interface lte get $Interface name ];
  :do {
    :set Firmware [ / interface lte firmware-upgrade $Interface once as-value ];
  } on-error={
    :log debug ("Could not get latest LTE firmware version for interface " . $IntName . ".");
  }

  :if (($Firmware->"installed") != ($Firmware->"latest")) do={
    :log info ("Scheduling LTE firmware upgrade for interface " . $IntName . ".");
    / system script add name=($IntName . "-firmware-upgrade") source=("# unattended-lte-firmware-upgrade\n" . \
       "/ system scheduler remove " . $IntName . "-firmware-upgrade;\n" . \
       "/ system script remove " . $IntName . "-firmware-upgrade;\n" . \
       "/ interface lte firmware-upgrade " . $IntName . " upgrade=yes;\n" . \
       ":log info (\"LTE firmware upgrade finished, waiting for installation before reset.\");\n" . \
       ":delay 150s;\n" . \
       "/ interface lte at-chat " . $IntName . " input=\"AT+RESET\";");
    / system scheduler add name=($IntName . "-firmware-upgrade") \
       on-event=("/ system script run " . $IntName . "-firmware-upgrade;") interval=1m;
  }
}
