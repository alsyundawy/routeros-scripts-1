#!rsc
# RouterOS script: collect-wireless-mac%TEMPL%
# Copyright (c) 2013-2019 Christian Hesse <mail@eworm.de>
#
# collect wireless mac adresses in access list
#
# !! This is just a template! Replace '%PATH%' with 'caps-man'
# !! or 'interface wireless'!

:global Identity;

:global GetMacVendor;
:global SendNotification;
:global ScriptLock;

$ScriptLock "collect-wireless-mac%TEMPL%";

:local PlaceBefore [ / %PATH% access-list find where comment="--- collected above ---" disabled ];
:if ([ :len $PlaceBefore ] = 0) do={
  :log error "Missing disabled access-list entry with comment '--- collected above ---'";
  :error "Error: See log for details.";
}

:foreach RegTbl in=[ / %PATH% registration-table find ] do={
  :local Mac [ / %PATH% registration-table get $RegTbl mac-address ];
  :local AccessList ([ / %PATH% access-list find where mac-address=$Mac ]->0);
  :if ([ :len $AccessList ] = 0) do={
    :local Address "no dhcp lease";
    :local DnsName "no dhcp lease";
    :local HostName "no dhcp lease";
    :local Lease [ / ip dhcp-server lease find where mac-address=$Mac ];
    :if ([ :len $Lease ] > 0) do={
      :set Address [ / ip dhcp-server lease get $Lease address ];
      :set HostName [ / ip dhcp-server lease get $Lease host-name ];
      :if ([ :len $HostName ] = 0) do={
        :set HostName "no hostname";
      }
      :set DnsName [ / ip dns static get ([ find where address=$Address ]->0) name ];
      :if ([ :len $DnsName ] = 0) do={
        :set DnsName "no dns name";
      }
    }
    :local RegEntry [ / %PATH% registration-table find where mac-address=$Mac ];
    :local Interface [ / %PATH% registration-table get $RegEntry interface ];
    :local Ssid [ / caps-man registration-table get $RegEntry ssid ];
    :local Ssid [ / interface wireless get [ find where name=$Interface ] ssid ];
    :local DateTime ([ / system clock get date ] . " " . [ / system clock get time ]);
    :local Vendor [ $GetMacVendor $Mac ];
    :local Message ("unknown MAC address " . $Mac . " (" . $Vendor . ", " . $HostName . ") " . \
      "first seen on " . $DateTime . " connected to SSID " . $Ssid . ", interface " . $Interface);
    / log info $Message;
    / %PATH% access-list add place-before=$PlaceBefore comment=$Message mac-address=$Mac disabled=yes;
    $SendNotification ($Mac . " connected to " . $Ssid) \
      ("A device with unknown MAC address connected to " . $Ssid . " on " . $Identity . ".\n\n" . \
        "Controller: " . $Identity . "\n" . \
        "Interface:  " . $Interface . "\n" . \
        "SSID:       " . $Ssid . "\n" . \
        "MAC:        " . $Mac . "\n" . \
        "Vendor:     " . $Vendor . "\n" . \
        "Hostname:   " . $HostName . "\n" . \
        "Address:    " . $Address . "\n" . \
        "DNS name:   " . $DnsName . "\n" . \
        "Date:       " . $DateTime);
  } else={
    :local Comment [ / %PATH% access-list get $AccessList comment ];
    :log debug ("MAC address " . $Mac . " already known: " . $Comment);
  }
}
