#!rsc
# RouterOS script: email-backup
# Copyright (c) 2013-2019 Christian Hesse <mail@eworm.de>
#
# create and email backup and config file

:global Identity;
:global Domain;
:global EmailBackupTo;
:global EmailBackupCc;
:global BackupSendBinary;
:global BackupSendExport;
:global BackupPassword;

:global CharacterReplace;
:global DeviceInfo;

:if ($BackupSendBinary != true && \
     $BackupSendExport != true) do={
  :log error ("Configured to send neither backup nor config export.");
  :error "Error: See log for details.";
}

# filename based on identity
:local FileName [ $CharacterReplace ($Identity . "." . $Domain) "." "_" ];
:local BackupFile "none";
:local ConfigFile "none";
:local Attach [ :toarray "" ];

# binary backup
:if ($BackupSendBinary = true) do={
  / system backup save encryption=aes-sha256 name=$FileName password=$BackupPassword;
  :set BackupFile ($FileName . ".backup");
  :set Attach ($Attach, $BackupFile);
}

# create configuration export
:if ($BackupSendExport = true) do={
  / export terse file=$FileName;
  :set ConfigFile ($FileName . ".rsc");
  :set Attach ($Attach, $ConfigFile);
}

# send email with status and files
/ tool e-mail send to=$EmailBackupTo cc=$EmailBackupCc \
    subject=("[" . $Identity . "] Backup & Config") \
    body=("Backup and config export for " . $Identity . ".\n\n" . \
      [ $DeviceInfo ] . "\n\n" . \
      "Backup file:    " . $BackupFile . "\n" . \
      "Config file:    " . $ConfigFile) \
    file=$Attach;
}
