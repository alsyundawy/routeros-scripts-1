#!rsc
# RouterOS script: gps-track
# Copyright (c) 2018-2020 Christian Hesse <mail@eworm.de>
#
# track gps data by sending json data to http server

:global GpsTrackUrl;
:global Identity;

:local CoordinateFormat [ /system gps get coordinate-format ];
:local Gps [ / system gps monitor once as-value ];

if ($Gps->"valid" = true) do={
  :tool fetch check-certificate=yes-without-crl \
    $GpsTrackUrl output=none \
    http-method=post http-header-field="Content-Type: application/json" \
    http-data=("{" . \
      "\"lat\":\"" . ($Gps->"latitude") . "\"," . \
      "\"lon\":\"" . ($Gps->"longitude") . "\"," . \
      "\"identity\":\"" . $Identity . "\"" . \
    "}");
  :log debug ("Sending GPS data in " . $CoordinateFormat . " format: " . \
    "lat: " . ($Gps->"latitude") . " " . \
    "lon: " . ($Gps->"longitude"));
} else={
  :log debug ("GPS data not valid.");
}
