#!rsc
# RouterOS script: ipv6-update
# Copyright (c) 2013-2019 Christian Hesse <mail@eworm.de>
#
# update firewall and dns settings on IPv6 prefix change

:local PdPrefix $"pd-prefix";

:global ParseKeyValueStore;

:if ([ :typeof $PdPrefix ] = "nothing") do={
  :log error "This script is supposed to run from ipv6 dhcp-client.";
  :error "Error: See log for details.";
}

:local Pool [ / ipv6 pool get [ find where prefix=$PdPrefix ] name ];
:local AddrList [ / ipv6 firewall address-list find where comment=("ipv6-pool-" . $Pool) ];
:local OldPrefix [ / ipv6 firewall address-list get $AddrList address ];

# give the interfaces a moment to receive their addresses
:delay 2s;

if ($OldPrefix != $PdPrefix) do={
  :log info ("Updating IPv6 address list with new IPv6 prefix " . $PdPrefix);
  / ipv6 firewall address-list set address=$PdPrefix $AddrList;

  :foreach Record in=[ / ip dns static find where comment~("^ipv6-pool-" . $Pool . ",") ] do={
    :local RecordVal [ / ip dns static get $Record ];
    :local Comment [ $ParseKeyValueStore ($RecordVal->"comment") ];

    :local Prefix [ / ipv6 address get [ find where interface=($Comment->"interface") from-pool=$Pool global ] address ];
    :set Prefix [ :pick $Prefix 0 [ :find $Prefix "::/64" ] ];
    :if ($Prefix~"^[0-9a-f]+:[0-9a-f]+:[0-9a-f]+:") do={ } else={
      :set Prefix ($Prefix . ":");
    }

    :log info ("Updating DNS record for " . ($RecordVal->"name") . ($RecordVal->"regexp") . \
        " to " . $Prefix . ":" . ($Comment->"suffix"));
    / ip dns static set address=($Prefix . ":" . ($Comment->"suffix")) $Record;
  }
}
