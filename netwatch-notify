#!rsc
# RouterOS script: netwatch-notify
# Copyright (c) 2020 Christian Hesse <mail@eworm.de>
#
# monitor netwatch and send notifications

:global NetwatchNotify;

:global ParseKeyValueStore;
:global LogPrintExit;
:global SendNotification;

:if ([ :typeof $NetwatchNotify ] = "nothing") do={
  :set NetwatchNotify [ :toarray "" ];
}

:foreach Host in=[ / tool netwatch find where comment~"^notify," ] do={
  :local HostVal [ / tool netwatch get $Host ];
  :local HostName ([ $ParseKeyValueStore ($HostVal->"comment") ]->"hostname");

  :local Metric { "count"=0; "notified"=false };
  :if ([ :typeof ($NetwatchNotify->$HostName) ] = "array") do={
    :set $Metric ($NetwatchNotify->$HostName);
  }

  :if ($HostVal->"status" = "up") do={
    $LogPrintExit debug ("Host " . $HostName . " (" . $HostVal->"host" . ") is up.") false;
    :set ($Metric->"count") 0;
    :if ($Metric->"notified" = true) do={
      $SendNotification ("Netwatch Notify: " . $HostName . " up") \
        ("Host " . $HostName . " (" . $HostVal->"host" . ") is up since " . $HostVal->"since" . ".");
    }
    :set ($Metric->"notified") false;
  } else={
    :set ($Metric->"count") ($Metric->"count" + 1);
    $LogPrintExit info ("Host " . $HostName . " (" . $HostVal->"host" . ") is down for " . \
      $Metric->"count" . " checks.") false;
    :if ($Metric->"count" >= 5 && $Metric->"notified" != true) do={
      $SendNotification ("Netwatch Notify: " . $HostName . " down") \
        ("Host " . $HostName . " (" . $HostVal->"host" . ") is down since " . $HostVal->"since" . ".");
      :set ($Metric->"notified") true;
    }
  }
  :set ($NetwatchNotify->$HostName) { "count"=($Metric->"count"); "notified"=($Metric->"notified") };
}
